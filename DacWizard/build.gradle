// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

description = 'application to configure a DynamicAnimControl'

mainClassName = 'jme3utilities.minie.wizard.DacWizard'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    runtimeOnly 'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Heart:' + heartVersion
    implementation 'com.github.stephengold:jme3-utilities-ui:' + jme3utilitiesuiVersion
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.13for33'
    implementation 'com.github.stephengold:Wes:0.4.8for33'
    runtimeOnly "org.jmonkeyengine:jme3-blender:$jmonkeyengineVersion"
    runtimeOnly "org.jmonkeyengine:jme3-desktop:$jmonkeyengineVersion"
    runtimeOnly "org.jmonkeyengine:jme3-jogg:$jmonkeyengineVersion"
    runtimeOnly "org.jmonkeyengine:jme3-plugins:$jmonkeyengineVersion"

    // select one version of LWJGL (from jcenter or mavenLocal)
    runtimeOnly "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengineVersion"  // LWJGL 2.x
    //runtimeOnly "org.jmonkeyengine:jme3-lwjgl3:$jmonkeyengineVersion" // LWJGL 3.x

    //implementation "jme3utilities:Minie:$minieVersion"
    implementation project(':MinieLibrary')
}

task runForceDialog(type: JavaExec) {
    args = ['--forceDialog']
    main mainClassName
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}
