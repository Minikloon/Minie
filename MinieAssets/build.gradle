// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

description = 'generate assets used in MinieExamples'
ext {
    assets = '../MinieExamples/src/main/resources/'

    duck = assets + 'Models/Duck'
    elephant = assets + 'Models/Elephant'
    mhGame = assets + 'Models/MhGame'
    ninja = assets + 'Models/Ninja'
    oto = assets + 'Models/Oto'
    sinbad = assets + 'Models/Sinbad'

    plaid = assets + 'Textures/plaid.png'
    torus = assets + 'CollisionShapes/torus.j3o'
}

dependencies {
    implementation "jme3utilities:jme3-utilities-heart:$heartVersion"
    runtimeOnly "org.jmonkeyengine:jme3-blender:$jmonkeyengineVersion"
    implementation "org.jmonkeyengine:jme3-desktop:$jmonkeyengineVersion"
    runtimeOnly "org.jmonkeyengine:jme3-jogg:$jmonkeyengineVersion"
    runtimeOnly "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengineVersion"
    implementation "org.jmonkeyengine:jme3-plugins:$jmonkeyengineVersion"
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    //implementation "jme3utilities:Minie:$minieVersion"
    implementation project(':MinieLibrary')
}

task collisionShapes {
    dependsOn = ['torus']
    description 'generate collision-shape assets used in MinieExamples'
}
task torus(type: JavaExec) {
    main 'jme3utilities.minie.test.shapes.MakeTorus'
    outputs.files([torus])
}

task models(type: JavaExec) {
    main 'jme3utilities.minie.test.models.ImportCgms'
    outputs.files([
            duck + '/Duck.j3o',
            elephant + '/Elephant.j3o',
            mhGame + '/MhGame.j3o',
            ninja + '/Ninja.j3o',
            oto + '/Oto.j3o',
            sinbad + '/Sinbad.j3o'
        ])
}

task textures {
    dependsOn = ['plaid']
    description 'generate texture assets used in MinieExamples'
}
task plaid(type: JavaExec) {
    main 'jme3utilities.minie.test.textures.MakePlaid'
    outputs.files([plaid])
}

// cleanup tasks
clean {
    dependsOn 'cleanDLLs'
    dependsOn 'cleanLogs'
    dependsOn 'cleanModels'
    dependsOn 'cleanSOs'
    dependsOn 'cleanTextures'
}
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanModels(type: Delete) {
    delete fileTree(dir: duck)
    delete fileTree(dir: elephant)
    delete fileTree(dir: mhGame)
    delete fileTree(dir: ninja)
    delete fileTree(dir: oto, exclude: 'OtoOldAnim.j3o')
    delete fileTree(dir: sinbad)
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanTextures(type: Delete) {
    delete plaid
}
