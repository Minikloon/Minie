// Gradle settings and tasks common to all Minie subprojects

apply plugin: 'java'

ext {
    jmeTarget = ''
    minieSnapshot = '-SNAPSHOT'

    // current versions of various libraries:
    heartVersion = '7.1.0' + jmeTarget
    jme3utilitiesuiVersion = '0.9.5' + jmeTarget
    jme3Version = '3.5.0-beta4'
    junitVersion = '4.13.2'
    minieVersion = '4.6.1'
    wesVersion = '0.6.7' + jmeTarget
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // Java runtime options:
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xcheck:jni'
    jvmArgs '-Xms1024m', '-Xmx1024m'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://jcenter.bintray.com' } // for SimMath
}

// cleanup tasks

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanPDBs')
clean.dependsOn('cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) { // Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) { // MacOSX native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanPDBs(type: Delete) { // Windows program database files
    delete fileTree(dir: '.', include: '*.pdb')
}
task cleanSOs(type: Delete) { // Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) { // jme3-utilities-ui output directory
    delete 'Written Assets'
}
